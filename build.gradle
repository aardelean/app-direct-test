buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.jacobono:gradle-jaxb-plugin:1.3.6'
    }
}

group 'home'
version '1.0'

apply plugin: 'java'
apply plugin: 'com.github.jacobono.jaxb'
project.ext.springBoot = '1.3.0.RELEASE'

project.ext.mainClassName = 'home.app.direct.Starter'

configurations{
    capsule
    jaxb
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "io.undertow:undertow-servlet:1.2.11.Final"
    compile ("io.undertow:undertow-core:1.2.11.Final"){
        exclude module: "jboss-logging"
    }
    compile "org.springframework.boot:spring-boot-starter:"+springBoot
    compile ("org.springframework.boot:spring-boot-starter-web:"+springBoot){
        exclude module: "spring-boot-starter-tomcat"
    }
    compile "org.springframework.boot:spring-boot-starter-thymeleaf:"+springBoot
    compile "org.springframework.boot:spring-boot-starter-data-rest:"+springBoot
    compile ("org.springframework.boot:spring-boot-starter-data-jpa:"+springBoot){
        exclude group: "org.hibernate"
    }
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'oauth.signpost:signpost-commonshttp4:1.2.1.2'
    compile "com.h2database:h2:1.4.188"
    compile "org.hibernate:hibernate-core:5.0.1.Final"
    compile "org.hibernate:hibernate-entitymanager:5.0.1.Final"
    testCompile 'junit:junit:4.11'
    testCompile 'org.springframework:spring-test:4.2.3.RELEASE'
    testCompile 'org.mockito:mockito-all:1.9.5'

    compile "org.projectlombok:lombok:1.16.4"

    capsule "co.paralleluniverse:capsule:1.0"
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.1.8'

}

jaxb {
    xsdDir = "src/main/resources/schemas"
    xjc {
        generatePackage    = "home.app.direct.transport"
    }
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "app-direct.jar"
    from jar // embed our application jar
    from { configurations.runtime } // embed dependencies

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' } // we just need the single Capsule class

    manifest {
        attributes(
                'Premain-Class'  :   'Capsule',
                'Main-Class'  :   'Capsule',
                'Application-Class'   : mainClassName,
                'Extract-Capsule' : 'false', // no need to extract the capsule
                'Min-Java-Version' : '1.8.0',
                'Dependencies': getDependencies(configurations.runtime).join(' ')
        )
    }
}

// converts Gradle dependencies to Capsule dependencies
def getDependencies(config) {
    return config.getAllDependencies().collect {
        def res = it.group + ':' + it.name + ':' + it.version +
                (!it.artifacts.isEmpty() ? ':' + it.artifacts.iterator().next().classifier : '')
        if(!it.excludeRules.isEmpty()) {
            res += "(" + it.excludeRules.collect { it.group + ':' + it.module }.join(',') + ")"
        }
        return res
    }
}